fs     = require 'fs'
{exec} = require 'child_process'
path   = require 'path'
http   = require 'http'

iswin = process.platform is 'win32'
currDir = __dirname
if iswin and currDir.substring(0,2) is "\\\\"
  depTarget = process.env.DEPLOYMENT_TARGET
  currDir = depTarget
coffee = path.join currDir,  "node_modules", "coffee-script", "bin", "coffee"
coffee = "node #{coffee}" if iswin

compileDirectory = (dirPath) ->
  for dirItem in fs.readdirSync(dirPath)
    fullItemPath = path.join dirPath, dirItem
    continue unless fs.existsSync fullItemPath
    isDirectory =  fs.statSync(fullItemPath).isDirectory()
    if isDirectory
        compileDirectory fullItemPath
    else
      compileFile fullItemPath

isACoffeeFile = (file) -> file.indexOf(".coffee", file.length - 7) isnt -1

compileFile = (file) ->
  return unless isACoffeeFile file
  console.log "found file #{file}, compiling..."
  exec "#{coffee} --compile #{file}", (err, stdout, stderr) ->
    throw err if err

createLink = (originFile, destFile) ->
  unless fs.existsSync destFile
    if iswin
      fs.writeFileSync destFile, fs.readFileSync(originFile)
    else
      fs.symlinkSync originFile, destFile

publicJSPath = (file) ->
  path.join(currDir, 'public', 'javascripts', 'lib', file)

nodeModulesPath = (paths...) ->
  returnPath = path.join(currDir, 'node_modules')
  for singlePath in paths
    returnPath = path.join(returnPath, singlePath)
  returnPath

createLinkToJSPath = (file) ->
  console.log "copying file #{path.basename file}..."
  createLink file, publicJSPath path.basename file

downloadFileToJS = (remoteFile, localFile) ->
  console.log "downloading file #{localFile}..."
  localFile = publicJSPath localFile
  return if fs.existsSync localFile
  file = fs.createWriteStream localFile
  request = http.get remoteFile, (response) -> response.pipe file

task 'build', 'Build single application file from source files', ->
  invoke 'compile:coffee'
  invoke 'jsdependencies'

task 'compile:coffee', 'compiles CoffeeScript files', ->
  compileDirectory currDir

task 'jsdependencies', 'copies js dependencies to public/script', ->
  createLinkToJSPath nodeModulesPath('backbone', 'backbone.js')
  createLinkToJSPath nodeModulesPath('backbone', 'node_modules', 'underscore', 'underscore.js')
  downloadFileToJS "http://ajax.googleapis.com/ajax/libs/jquery/1.9.1/jquery.min.js", "jquery.min.js"
