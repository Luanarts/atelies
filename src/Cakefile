fs            = require 'fs'
{exec}        = require 'child_process'
path          = require 'path'
http          = require 'http'
child_process = require 'child_process'

iswin = process.platform is 'win32'
currDir = __dirname
if iswin and currDir.substring(0,2) is "\\\\"
  depTarget = process.env.DEPLOYMENT_TARGET
  currDir = depTarget
coffee = path.join currDir,  "node_modules", "coffee-script", "bin", "coffee"
coffee = "node #{coffee}" if iswin

compileDirectory = (dirPath) ->
  for dirItem in fs.readdirSync(dirPath)
    fullItemPath = path.join dirPath, dirItem
    continue unless fs.existsSync fullItemPath
    isDirectory =  fs.statSync(fullItemPath).isDirectory()
    if isDirectory
        compileDirectory fullItemPath
    else
      compileFile fullItemPath

isACoffeeFile = (file) -> file.indexOf(".coffee", file.length - 7) isnt -1

compileFile = (file) ->
  return unless isACoffeeFile file
  console.log "found file #{file}, compiling..."
  exec "#{coffee} --compile #{file}", (err, stdout, stderr) ->
    throw err if err

publicJSPath = (file) ->
  path.join(currDir, 'public', 'javascripts', 'lib', file)

downloadFileToJS = (remoteFile, localFile) ->
  console.log "downloading file #{localFile}..."
  localFile = publicJSPath localFile
  return if fs.existsSync localFile
  file = fs.createWriteStream localFile
  request = http.get remoteFile, (response) -> response.pipe file

task 'build', 'Build single application file from source files', ->
  invoke 'dependencies:npm'
  invoke 'compile:coffee'
  invoke 'dependencies:js'

task 'dependencies:npm', 'Run npm', ->
  console.log 'Installing npm packages...'
  npmProcess = child_process.spawn 'npm', ['install'], { cwd: currDir, env: process.env }
  npmProcess.stdout.pipe process.stdout
  npmProcess.stderr.pipe process.stderr

task 'compile:coffee', 'compiles CoffeeScript files', ->
  compileDirectory currDir

task 'dependencies:js', 'copies js dependencies to public/script', ->
  downloadFileToJS "http://raw.github.com/documentcloud/underscore/master/underscore.js", "underscore.js"
  downloadFileToJS "http://raw.github.com/documentcloud/backbone/master/backbone.js", "backbone.js"
  downloadFileToJS "http://ajax.googleapis.com/ajax/libs/jquery/1.9.1/jquery.min.js", "jquery.min.js"
